plugins {
    id 'org.jetbrains.kotlin.jvm' version "$kotlinVersion"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(javaVersion)
    }
}

allprojects {
    repositories {
        mavenCentral()
        maven { url 'https://cache-redirector.jetbrains.com/kotlin.bintray.com/kotlin-plugin' }
    }
}

configure(subprojects.findAll { it.name != 'grammars' }) {
    apply plugin: 'kotlin'
    apply plugin: 'maven-publish'

    publishing {
        repositories {
            maven {
                name = 'GitHubPackages'
                url = uri('https://maven.pkg.github.com/fwcd/kotlin-language-server')
                credentials {
                    username = System.getenv('GITHUB_ACTOR')
                    password = System.getenv('GITHUB_TOKEN')
                }
            }
        }

        publications {
            gpr(MavenPublication) {
                from components.java
            }
        }
    }
}
