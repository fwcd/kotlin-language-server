import org.gradle.plugins.ide.eclipse.model.ClasspathEntry
import org.gradle.plugins.ide.eclipse.model.EclipseModel
import org.gradle.plugins.ide.eclipse.model.Library

allprojects { project ->
    apply plugin: "eclipse"

    task kotlinLSPProjectDeps { task ->
        doLast {
            def classpaths = new HashMap()
            System.out.println ""
            System.out.println "gradle-version $gradleVersion"
            System.out.println "kotlin-lsp-project ${project.name}"

            if (project.hasProperty('android')) {
                project.android.getBootClasspath().each {
                    System.out.println "kotlin-lsp-gradle $it"
                }

                def variants = []

                if (project.android.hasProperty('applicationVariants')) {
                    variants += project.android.applicationVariants
                }

                if (project.android.hasProperty('libraryVariants')) {
                    variants += project.android.libraryVariants
                }

                variants.each { variant ->
                    def variantBase = variant.baseName.replaceAll("-", File.separator)

                    def buildClasses = project.getBuildDir().absolutePath +
                        File.separator + "intermediates" +
                        File.separator + variantBase +
                        File.separator + "classes"

                    classpaths[buildClasses.toString()] = null

                    def userClasses = project.getBuildDir().absolutePath +
                        File.separator + "intermediates" +
                        File.separator + "javac" +
                        File.separator + variantBase +
                        File.separator + "compile" + variantBase.capitalize() + "JavaWithJavac" + File.separator + "classes"

                    classpaths[userClasses.toString()] = null

                    def userVariantClasses = project.getBuildDir().absolutePath +
                        File.separator + "intermediates" +
                        File.separator + "javac" +
                        File.separator + variantBase +
                        File.separator + "classes"

                    classpaths[userVariantClasses.toString()] = null

                    variant.getCompileClasspath().each {
                        classpaths[it.toString()] = null
                    }
                }


            } else if (project.hasProperty('sourceSets')) {
                // Print the list of all dependencies jar files.
                sourceSets.forEach {
                    it.compileClasspath.forEach {
                        classpaths[it.toString()] = null
                    }
                }
            }


            // handle kotlin multiplatform style dependencies if any
            def kotlinExtension = project.extensions.findByName("kotlin")
            if(kotlinExtension && kotlinExtension.hasProperty("targets")) {
                def kotlinSourceSets = kotlinExtension.sourceSets

                // Print the list of all dependencies jar files.
                kotlinExtension.targets.names.each {
                    def classpath = configurations["${it}CompileClasspath"]
                    classpath.files.each {
                        classpaths[it.toString()] = null
                    }
                }
            }

            EclipseModel eclipseModel = project.extensions.getByType(EclipseModel.class)

            for (ClasspathEntry dep : (eclipseModel.getClasspath().resolveDependencies())) {
                if (dep instanceof Library) {
                    if (classpaths.get(dep.getPath()) == null){
                        classpaths[dep.getPath()] = dep.getSourcePath()?.getPath()
                    }
                }
            }

            for (e in classpaths) {
                System.out.println "kotlin-lsp-gradle path:$e.key source:$e.value"
            }
        }
    }

    task kotlinLSPAllGradleDeps {
        doLast {
            fileTree("$gradle.gradleHomeDir/lib")
                .findAll { it.toString().endsWith '.jar' }
                .forEach { System.out.println "kotlin-lsp-gradle path:$it source:null" }
        }
    }
}
